// MIT Licensed (see LICENSE.md).

struct SamplerHelper
{
  [Static]
  function ConvertUvCoordinate(uv : Real2) : Real2
  {
    if (ShaderIntrinsics.IsLanguage(ShaderLanguage.Glsl))
      uv.Y = 1.0 - uv.Y;
    return uv;
  }

  [Static]
  function ConvertUvCoordinate(uv : Real3) : Real3
  {
    if (ShaderIntrinsics.IsLanguage(ShaderLanguage.Glsl))
      uv.X = -uv.X;
    return uv;
  }
}

struct SampledImageExtensions
{
  [Extension(typeid(SampledImage2d))]
  function Sample(uv : Real2) : Real4
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleImplicitLod(*this, uv);
  }
  
  [Extension(typeid(SampledImage2d))]
  function Sample(uv : Real2, ddx : Real2, ddy : Real2) : Real4
  {
    // Need to determine if it is technically correct
    // to y-invert the derivatives when the uv is.
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleGradExplicitLod(*this, uv, ddx, ddy);
  }
  
  [Extension(typeid(SampledImage2d))]
  function SampleLod(uv : Real2, lod : Real) : Real4
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleExplicitLod(*this, uv, lod);
  }
  
  [Extension(typeid(SampledImage2d))]
  function SampleSize(lod : Integer) : Integer2
  {
    return ShaderIntrinsics.ImageQuerySize(*this, lod);
  }
}

struct SampledDepthImageExtensions
{
  [Extension(typeid(SampledDepthImage2d))]
  function Sample(uv : Real2, depthCompare : Real) : Real
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleDRefImplicitLod(*this, uv, depthCompare);
  }
  
  [Extension(typeid(SampledDepthImage2d))]
  function SampleLod(uv : Real2, depthCompare : Real, lod : Real) : Real
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleDRefExplicitLod(*this, uv, depthCompare, lod);
  }
  
  [Extension(typeid(SampledDepthImage2d))]
  function SampleSize(lod : Integer) : Integer2
  {
    return ShaderIntrinsics.ImageQuerySize(*this, lod);
  }
}

struct SampledImageCubeExtensions
{
  [Extension(typeid(SampledImageCube))]
  function Sample(uv : Real3) : Real4
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleImplicitLod(*this, uv);
  }
  
  [Extension(typeid(SampledImageCube))]
  function SampleLod(uv : Real3, lod : Real) : Real4
  {
    uv = SamplerHelper.ConvertUvCoordinate(uv);
    return ShaderIntrinsics.SampleExplicitLod(*this, uv, lod);
  }
  
  [Extension(typeid(SampledImageCube))]
  function SampleSize(lod : Integer) : Integer2
  {
    return ShaderIntrinsics.ImageQuerySize(*this, lod);
  }
}
